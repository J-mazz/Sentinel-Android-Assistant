# Intent Classification Grammar

root ::= "{" ws intent-field "," ws confidence-field "," ws entities-field ("," ws reasoning-field)? ws "}"

intent-field ::= "\"intent\"" ws ":" ws intent-value

intent-value ::= "\"READ_CALENDAR\"" | "\"CREATE_EVENT\"" | "\"UPDATE_EVENT\"" | "\"DELETE_EVENT\"" |
                "\"CREATE_ALARM\"" | "\"LIST_ALARMS\"" | "\"DELETE_ALARM\"" |
                "\"CALL_CONTACT\"" | "\"SEND_SMS\"" |
                "\"CLICK_ELEMENT\"" | "\"SCROLL_SCREEN\"" | "\"TYPE_TEXT\"" | "\"GO_BACK\"" | "\"GO_HOME\"" |
                "\"SEARCH\"" | "\"ANSWER_QUESTION\"" |
                "\"SEARCH_SELECTED\"" | "\"TRANSLATE_SELECTED\"" | "\"COPY_SELECTED\"" | "\"SAVE_SELECTED\"" | "\"SHARE_SELECTED\"" | "\"EXTRACT_DATA_FROM_SELECTION\"" |
                "\"UNKNOWN\""

confidence-field ::= "\"confidence\"" ws ":" ws number

entities-field ::= "\"entities\"" ws ":" ws object

reasoning-field ::= "\"reasoning\"" ws ":" ws string

# JSON primitives
object ::= "{" ws (pair ("," ws pair)*)? ws "}"

pair ::= string ws ":" ws value

value ::= string | number | object | array | "true" | "false" | "null"

array ::= "[" ws (value ("," ws value)*)? ws "]"

string ::= "\"" characters "\""

characters ::= character*

character ::= [^"\\] | "\\" escape-char

escape-char ::= ["\\nrt/]

number ::= integer ("." digits)?

integer ::= "-"? ("0" | [1-9] digits?)

digits ::= [0-9]+

ws ::= [ \t\n\r]*
